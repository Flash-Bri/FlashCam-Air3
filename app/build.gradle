plugins {
    id 'com.android.application'
}

// Load signing properties from keystore.properties (not committed)
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'com.flashcam.air3'
    compileSdk 34

    defaultConfig {
        applicationId "com.flashcam.air3"
        minSdk 31
        targetSdk 34
        versionCode 2
        versionName "1.4"
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'] ?: '../release-key.jks')
                storePassword keystoreProperties['storePassword'] ?: System.getenv('KEYSTORE_PASSWORD') ?: ''
                keyAlias keystoreProperties['keyAlias'] ?: System.getenv('KEY_ALIAS') ?: ''
                keyPassword keystoreProperties['keyPassword'] ?: System.getenv('KEY_PASSWORD') ?: ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.core:core:1.12.0'
}
